plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'fabric-loom' version '1.4-SNAPSHOT'
    id 'io.github.0ffz.github-packages' version '[1,2)'
}

version = "${mc_version}-${mod_version}-fabric"
group = 'com.aetherteam.nitrogen'

base {
    archivesName = mod_id
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

sourceSets {
    main {
        resources {
            srcDirs += [ 'src/main/generated' ]
        }
    }
}

loom {
    runs {
        datagen {
            inherit client
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=${project.mod_id}"

            runDir "build/datagen"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.mc_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${project.mc_version}:${project.parchment_mappings}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

    modImplementation(include("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.components_version}"))
    modImplementation(include("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.components_version}"))
    modImplementation(include("dev.onyxstudios.cardinal-components-api:cardinal-components-world:${project.components_version}"))

    for (String module in project.porting_lib_modules.split(",")) {
        modApi(include("io.github.fabricators_of_create.Porting-Lib:$module:${project.porting_lib_version}"))
    }

//    compileOnly "mezz.jei:jei-${project.mc_version}-common:${project.jei_version}"
//    compileOnly "mezz.jei:jei-${project.mc_version}-forge:${project.jei_version}"
}

repositories {
    maven { url = "https://maven.blamejared.com" }
    maven { url = "https://maven.parchmentmc.org" }
    maven { url = "https://jitpack.io" }
    maven { url = "https://maven.shedaniel.me/" }
    maven { url = "https://mvn.devos.one/snapshots/" }
    maven { url = "https://maven.ladysnake.org/releases" }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : project.mod_name,
                "Specification-Vendor"    : "The Aether Team",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.mod_name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "The Aether Team",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

compileJava {
    options.incremental = false
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/The-Aether-Team/Nitrogen"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}